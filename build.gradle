/*
 * Copyright 2019 Amazon.com, Inc. or its affiliates.
 * Licensed under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
  id 'java'
  id 'maven-publish'
  id "io.freefair.lombok" version "6.2.0"
  id 'signing'
}

group = "software.amazon.rdsdata"
version = "1.0.8"
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def isReleaseVersion = !version.endsWith('SNAPSHOT')

repositories {
  mavenCentral()
}

dependencies {
  implementation 'com.amazonaws:aws-java-sdk-rdsdata:1.11.970'
  implementation 'com.google.guava:guava:28.1-jre'

  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
  testImplementation 'org.junit.jupiter:junit-jupiter-params:5.5.2'
  testImplementation 'org.mockito:mockito-core:3.1.0'
  testImplementation 'org.assertj:assertj-core:3.13.2'
}

test {
  useJUnitPlatform()
}

tasks.withType(JavaCompile) {
  options.compilerArgs << '-parameters'
}

publishing {
  repositories {
    maven {
      name = 'sonatype'
      url = isReleaseVersion
              ? 'https://aws.oss.sonatype.org/service/local/staging/deploy/maven2/'
              : 'https://aws.oss.sonatype.org/content/repositories/snapshots/'
      credentials {
        username findProperty('sonatype.username')
        password findProperty('sonatype.password')
      }
    }
  }

  publications {
    mavenJava(MavenPublication) {
      pom {
        name = 'RDS Data API Client Library'
        description = 'Java client library for AWS RDS Data API'
        url = 'https://github.com/awslabs/rds-data-api-client-library-java'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = 'amazonwebservices'
            name = 'Amazon Web Services'
            url = 'https://aws.amazon.com'
          }
        }
        scm {
          connection = 'scm:git:https://github.com/awslabs/rds-data-api-client-library-java.git'
          developerConnection = 'scm:git:git@github.com:awslabs/rds-data-api-client-library-java.git'
          url = 'https://github.com/awslabs/rds-data-api-client-library-java'
        }
      }

      from components.java
    }
  }
}

java {
  withSourcesJar()
  withJavadocJar()
}

signing {
  sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
  onlyIf { isReleaseVersion }
}